plugins {
    id 'fabric-loom' version "$loom_version"
    id 'maven-publish'
}

group 'dev.lazurite'
version lattice_version

// done prior to dependencies so testmodImplementation exists
sourceSets {
    testmod {
        compileClasspath += sourceSets.main.compileClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
}

repositories {
    maven {
        name 'Lazurite'
        url 'https://lazurite.dev/releases'
    }
}

dependencies {
    mappings loom.officialMojangMappings()
    minecraft "com.mojang:minecraft:$minecraft_version"
    modImplementation "net.fabricmc:fabric-loader:$fabric_loader_version"

    modImplementation "dev.lazurite:toolbox-fabric:$toolbox_version"

    testmodImplementation sourceSets.main.output
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of min_java_version
    withSourcesJar()
}

jar {
    from 'LICENSE'
}

loom {
    accessWidenerPath = file 'src/main/resources/lattice.accessWidener'

    runs {
        testmodClient {
            client()
            name "Testmod Client"
            source sourceSets.testmod
        }

        testmodServer {
            server()
            name "Testmod Server"
            source sourceSets.testmod
        }
    }
}

tasks.withType(ProcessResources) {
    inputs.properties(
            'version': project.version,

            'min_java_version': min_java_version,
            'min_minecraft_version': min_minecraft_version,
            'min_fabric_loader_version': min_fabric_loader_version
    )

    filesMatching 'fabric.mod.json', {
        expand(
                'version': project.version,

                'min_java_version': min_java_version,
                'min_minecraft_version': min_minecraft_version,
                'min_fabric_loader_version': min_fabric_loader_version
        )
    }

    filesMatching 'lattice.*.mixins.json', {
        expand 'min_java_version': min_java_version
    }
}

publishing {
    repositories {
        maven {
            name 'Lazurite'
            url 'https://lazurite.dev/releases'

            credentials {
                username property('publish.lazurite.alias')
                password property('publish.lazurite.secret')
            }
        }
    }

    publications {
        lattice MavenPublication, {
            from components.java
        }
    }
}
