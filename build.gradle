plugins {
    id 'fabric-loom' version '0.8-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

minecraft {
    accessWidener "src/main/resources/accesswidener/lattice.accesswidener"
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

compileJava {
    options.encoding = "UTF-8"
    it.options.release = 16
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

java {
    withSourcesJar()
}

jar {
    from "LICENSE"
}

remapJar { }

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'lattice'

            pom {
                name = 'Lattice'
                description = "A library mod that allows for chunk generation, loading, and rendering around the player's camera entity."
                url = 'https://github.com/LazuriteMC/Lattice'
                developers {
                    developer {
                        id = 'innatebit'
                        name = 'InnateBit'
                        email = 'patrickhofmanndesigns@gmail.com'
                    }
                }
            }

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            artifact(remapJar) {
                builtBy remapJar
            }

            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        mavenLocal()

        maven {
            url = 'https://lazurite.dev/maven/releases'
            credentials {
                username = project.property("publish.lazurite.alias")
                password = project.property("publish.lazurite.secret")
            }
        }
    }
}