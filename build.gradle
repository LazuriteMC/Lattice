plugins {
    id 'fabric-loom' version "$loom_version"
    id 'maven-publish'
}

group 'dev.lazurite'
version lattice_version

repositories {
    maven {
        name 'Lazurite'
        url 'https://lazurite.dev/releases'
    }
}

dependencies {
    mappings loom.officialMojangMappings()
    minecraft "com.mojang:minecraft:$minecraft_version"
    modImplementation "net.fabricmc:fabric-loader:$fabric_loader_version"

    modApi "dev.lazurite:toolbox-fabric:$toolbox_version"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of 17
    withSourcesJar()
}

jar {
    from 'LICENSE'
}

loom {
    accessWidenerPath = file 'src/main/resources/lattice.accesswidener'
}

tasks.withType(ProcessResources) {
    inputs.properties 'version': project.version

    filesMatching 'fabric.mod.json', {
        expand 'version': project.version
    }
}

publishing {
    if (hasProperty('publish.lazurite.alias') && hasProperty('publish.lazurite.secret')) {
        repositories {
            maven {
                name 'Lazurite'
                url 'https://lazurite.dev/releases'

                credentials {
                    username property('publish.lazurite.alias')
                    password property('publish.lazurite.secret')
                }
            }
        }
    }

    publications {
        lattice MavenPublication, {
            from components.java
        }
    }
}
